buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "nl.javadude.gradle.plugins:license-gradle-plugin:0.11.0"
    }
}

defaultTasks 'check'

version = '0.0.3'
group = 'com.xebialabs.overthere'

apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'license'

repositories {
    mavenLocal()
    mavenCentral()
}

sourceCompatibility = 1.7
targetCompatibility = 1.7


dependencies {
    compile "com.xebialabs.overthere:overthere:2.4.4"
    compile "org.python:jython:2.5.3"
    testCompile "junit:junit:4.11"
}

sourceSets.main.resources.srcDirs = ["src/main/jython", "src/main/resources"]
sourceSets.test.resources.srcDirs = ["src/test/jython", "src/test/resources"]

task testJython(type: Exec, dependsOn: ['testClasses']) {
    environment = ["CLASSPATH": sourceSets.test.runtimeClasspath.asPath, "PATH": System.getenv("PATH"), "HOME": System.getenv("HOME")]
    commandLine jythonInterpreter, "$projectDir/src/test/jython/xlnoserunner/runtests.py", "$projectDir/src/test/jython/test_overtherepy", buildDir
    workingDir = projectDir
}

test.dependsOn testJython

license {
    header rootProject.file('src/main/license/xebialabs_community.license')
    strictCheck true
    exclude('testfiles/*')
    exclude('directorycompare/*')
}


task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar, javadocJar
}

license {
    header project.file('HEADER')
    strictCheck true
    exclude('testfiles/*')
    exclude('directorycompare/*')
}

signing {
    required { gradle.taskGraph.hasTask('uploadArchives') }
    sign configurations.archives
}

uploadArchives {
    if(project.hasProperty('sonatypeUsername')) {
        repositories.mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            configuration = configurations.archives

            repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2') {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }
            snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots/') {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
                name 'Overtherepy'
                description 'Jython wrapper for Overthere'
                packaging 'jar'
                url 'https://github.com/xebialabs-community/overthere-pylib'
                inceptionYear '2014'

                scm {
                    url 'https://github.com/xebialabs-community/overthere-pylib'
                }

                licenses {
                    license {
                        name 'GPLv2 with XebiaLabs FLOSS License Exception'
                        url 'https://github.com/xebialabs/overthere/blob/master/LICENSE'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'rnaidoo'
                        name 'Ravan Naidoo'
                        url 'http://www.xebialabs.com/'
                        email 'rnaidoo@xebialabs.com'
                        roles {
                            role 'Developer'
                        }
                    }
                }
            }
        }
    }
}
